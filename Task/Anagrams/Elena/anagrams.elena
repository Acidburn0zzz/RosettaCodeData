import system'routines.
import system'calendar.
import system'io.
import system'collections.
import extensions.
import extensions'routines.

extension op
{
     normalized
         = self toArray; ascendant; summarize(String new); literal.
}

symbol program =
[
    var aDictionary := Dictionary new.

    File new("unixdict.txt"); forEachLine(:aWord)
    [
        var s := aWord.
        var aKey := aWord normalized.
        var anItem := aDictionary[aKey].
        if ($nil == anItem)
        [
            anItem := ArrayList new.
            aDictionary[aKey] := anItem.
        ].

        anItem append:aWord.
    ].

    aDictionary array_list;
        sort(:aFormer:aLater)( aFormer length > aLater length );
        top:20; forEach(:aPair)[ console printLine(aPair value) ].

    console readChar.
].
