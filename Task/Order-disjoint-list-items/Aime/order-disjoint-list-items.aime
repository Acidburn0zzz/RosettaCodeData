void
order(integer m, ...)
{
    integer i, j;
    record r;
    text s;

    ocall(o_, 0, 1, m, " ");

    o_("| ");

    i = (j = m) + 1;
    while (i < count()) {
        r[s = $i] += 1;
        o_(s, " ");
        i += 1;
    }

    o_("->");

    i = 0;
    do {
        i += 1;
        if ((r[s = $i] += -1) < 0) {
            o_(" ", s);
        } else {
            o_(" ", $(j += 1));
        }
    } while (i < m);

    o_newline();
}

integer
main(void)
{
    order(6, "the", "cat", "sat", "on", "the", "mat", "mat", "cat");
    order(6, "the", "cat", "sat", "on", "the", "mat", "cat", "mat");
    order(9, "A", "B", "C", "A", "B", "C", "A", "B", "C", "C", "A", "C", "A");
    order(9, "A", "B", "C", "A", "B", "D", "A", "B", "E", "E", "A", "D", "A");
    order(2, "A", "B", "B");
    order(2, "A", "B", "B", "A");
    order(4, "A", "B", "B", "A", "B", "A");

    return 0;
}
